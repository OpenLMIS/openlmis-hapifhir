buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // the specific version of spring boot is used to make sure that this service
        // uses the same version of spring boot as the HAPI FHIR.
        // https://github.com/jamesagnew/hapi-fhir/blob/master/pom.xml#L531-L534
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE"
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.2.0"
    id "org.sonarqube" version "2.6.2"
    id "com.moowork.node" version "1.2.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'pmd'

group = serviceGroup
version = serviceVersion
archivesBaseName = rootProject.name
project.ext.buildTime = java.time.Instant.now().toString() // for versioning
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

// Spring boot loads a different version of spring. However, HAPI FHIR has dependancy on v5.0.6
// the block of code ensures the right version of spring is used with spring boot.
configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == "org.springframework") {
                details.useVersion "5.0.6.RELEASE"
            }
        }
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.projectlombok:lombok:1.16.8"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.postgresql:postgresql:42.0.0"

    compile "org.springframework:spring-test"
    compile 'org.webjars.npm:api-console:3.0.17'
    compile 'org.openlmis:openlmis-service-util:3.0.0'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.flywaydb:flyway-core:4.0.3'
    compile "org.springframework.security.oauth:spring-security-oauth2"
    compile "org.slf4j:slf4j-ext:1.7.25"

    compile 'org.thymeleaf:thymeleaf-spring5:3.0.9.RELEASE'

    compile 'ca.uhn.hapi.fhir:hapi-fhir-base:3.4.0'
    compile 'ca.uhn.hapi.fhir:hapi-fhir-jpaserver-base:3.4.0'
    compile 'ca.uhn.hapi.fhir:hapi-fhir-client-okhttp:3.4.0'
    compile 'ca.uhn.hapi.fhir:hapi-fhir-jaxrsserver-base:3.4.0'
    compile 'org.apache.commons:commons-dbcp2:2.1.1'


    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.6'

    compile 'org.springframework:spring-orm'
    compile 'org.hibernate:hibernate-core:5.3.6.Final'
    compile 'org.hibernate:hibernate-entitymanager:5.3.6.Final'

    compile 'com.bedatadriven:jackson-datatype-jts:2.2'

    testCompile "junit:junit"
    testCompile "org.mockito:mockito-core:1.+"
    testCompile "org.springframework.boot:spring-boot-starter-test"

    testCompile "com.jayway.restassured:rest-assured:2.7.0"
    testCompile "guru.nidi.raml:raml-tester:0.8.9"

    testCompile "nl.jqno.equalsverifier:equalsverifier:2.4"
    testCompile "be.joengenduvel.java.verifiers:to-string:1.0.2"
}

idea {
    project {
        vcs = 'Git'
        ipr.withXml { xmlFile ->
            // enable 'Annotation Processors', source: https://gist.github.com/stephanos/8645809
            xmlFile.asNode().component
                    .find { it.@name == 'CompilerConfiguration' }['annotationProcessing'][0]
                    .replaceNode {
                annotationProcessing {
                    profile(default: true, name: 'Default', useClasspath: 'true', enabled: true)
                }
            }
        }
    /*    iml.withXml { xmlFile ->
            xmlFile.asNode().component.find { it.@name == 'NewModuleRootManager'}['Content'][0]
                .addNode {
                sourceFolder {
                    profile(url: "file://$MODULE_DIR\$/src/integration-test/java", isTestSource:
                            true)
                }
            }
        }*/
    }
}

flyway {
    url = "$System.env.DATABASE_URL"
    user = "$System.env.POSTGRES_USER"
    password = "$System.env.POSTGRES_PASSWORD"
    schemas = ['hapifhir']
    sqlMigrationPrefix = ''
    placeholderPrefix = '#['
    placeholderSuffix = ']'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }

    main {
        resources {
            srcDirs "src/main/resources", "build/data"
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
    mustRunAfter test
    environment 'BASE_URL', "http://localhost"
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
    reports.junitXml.destination = file("${testResultsDir}/${name}")
}

// Usage: gradle generateMigration [-PmigrationName=name_of_migration]
//          Defaults to 'migration' as migration name
// Example: gradle generateMigration -PmigrationName=add_column_to_users
//          Will create a file in migration folder with name yyyyMMddHHmmssSSS_add_column_to_users.sql.
task generateMigration << {
    description 'Creates an empty new file within the src/main/resources/db/migration directory into which developers can add new SQL migration code.'
    def fileName = project.hasProperty('migrationName') ? migrationName : 'migration'
    def timestamp = new Date().format('yyyyMMddHHmmssSSS', TimeZone.getTimeZone('GMT'))
    def fullFileName = "${timestamp}__${fileName}.sql"
    def migrationFile = new File(sourceSets.main.resources.srcDirs.first(),'db/migration/'+fullFileName)
    migrationFile.createNewFile()
}

task demoDataSeed(type:Exec) {
    executable "./demo-data/demo_seed.sh"
    args "demo-data", "build/data/demo-data", "demo-data/generator.js"
}

task checkApiIsRaml(type:Exec) {
    executable "raml-cop"
    args "src/main/resources/api-definition.yaml"
}

configure(checkApiIsRaml) {
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = 'Verify that the api-specification is valid RAML'
}

jacocoTestReport {
    group = "reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled false
        html.enabled true
        csv.enabled false
    }

    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}

checkstyle {
    toolVersion = "8.12"
}

//Usage: gradle sonarqube
sonarqube {
    properties {
        def branch = System.getenv("SONAR_BRANCH")
        if (branch && branch != 'master') {
            property "sonar.branch", branch
        }

        property "sonar.projectName", "OpenLMIS HAPI FHIR Service"
        property "sonar.projectKey", "org.sonarqube:$rootProject.name"
        property "sonar.host.url", "http://sonar.openlmis.org"
        property "sonar.login", System.getenv("SONAR_LOGIN")
        property "sonar.password", System.getenv("SONAR_PASSWORD")
        property "sonar.projectVersion", version
        property "sonar.java.coveragePlugin", "jacoco"
        //Tells SonarQube where the unit tests execution reports are
        property "sonar.junit.reportsPath", "build/test-results/test"
        //Tells SonarQube where the unit tests code coverage report is
        property "sonar.jacoco.reportPath", "build/jacoco/test.exec"
        //Tells SonarQube where the integration tests code coverage report is
        property "sonar.jacoco.itReportPath", "build/jacoco/integrationTest.exec"
        properties["sonar.tests"] += sourceSets.integrationTest.java
    }
}

project.tasks["sonarqube"].dependsOn integrationTest

pmd {
    toolVersion = '5.4.0'
    consoleOutput= true
    ignoreFailures = false
    ruleSetFiles = files("config/pmd/ruleset.xml")
    reportsDir = file("build/reports/pmd")
}

tasks.withType(Pmd){
    reports {
        xml.enabled true
        html.enabled true
    }
}


test {
    testLogging {
        events 'started', 'passed'
        exceptionFormat = 'full'
    }
}

apply from: "documentation.gradle"
integrationTest.dependsOn copyRamlToBuild

processResources {
    dependsOn demoDataSeed

    // we want the generated HTML spec file included in the output jar
    finalizedBy ramlToHtml

    // update version information in build
    filesMatching('**/version.properties') {
        expand(project.properties)
    }
}

apply from: "registration.gradle"

assemble {
    dependsOn ramlToHtml
    dependsOn copyRamlHtmlToBuild
    dependsOn copyConsulRegistrationToBuild
    dependsOn jacocoTestReport
}

check {
    dependsOn ramlToHtml
    dependsOn copyRamlHtmlToBuild
    dependsOn integrationTest
    dependsOn copyConsulRegistrationToBuild
}

build {
    dependsOn jacocoTestReport
    dependsOn check
}
